*Trova il minimo all'interno di un vettore di word con segno utilizzando stack frame 
* passando come parametri l'indirizzo del primo elemento del vettore,
* la dimensione del vettore e lasciando spazio per il risultato restituito dalla subroutine.
	ORG	$8100
MAIN	MOVE.L	#VET,-(A7)
	MOVE.W	DIM,-(A7)
	SUB.L	#2,A7
	JSR	SUBR
	MOVE.W	(A7)+,RIS
	ADD.L	#6,A7
FINE	BRA	FINE
	ORG	$8200
SUBR	LINK	A6,#0
	MOVEM.L	D0-D2/A0,-(A7)
	MOVE.W	#$7FFF,D0
	MOVEA.L	12(A6),A0
	MOVE.W	10(A6),D1
LOOP	CMP.W	#0,D1
	BLE	ESCI
	MOVE.W	(A0)+,D2
	CMP	D0,D2
	BGE	SKIP
	MOVE.W	D2,D0
SKIP	SUB.W	#1,D1
	BRA	LOOP
ESCI	MOVE.W	D0,8(A6)
	MOVEM.L	(A7)+,D0-D2/A0
	UNLK	A6
	RTS
	ORG	$8000
VET	DC.W	54,543,-54,35,0,43,54,87,0,-3
DIM	DC.W	10
RIS	DS.W	1	

	END	MAIN
